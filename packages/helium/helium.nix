{
  lib,
  stdenv,
  fetchurl,
  makeWrapper,
  bintools,
  patchelf,
  copyDesktopItems,
  makeDesktopItem,
  # Linked dynamic libraries.
  alsa-lib,
  at-spi2-atk,
  at-spi2-core,
  atk,
  cairo,
  cups,
  dbus,
  expat,
  fontconfig,
  freetype,
  gcc-unwrapped,
  gdk-pixbuf,
  glib,
  gtk3,
  gtk4,
  libdrm,
  libglvnd,
  libkrb5,
  libX11,
  libxcb,
  libXcomposite,
  libXcursor,
  libXdamage,
  libXext,
  libXfixes,
  libXi,
  libxkbcommon,
  libXrandr,
  libXrender,
  libXScrnSaver,
  libxshmfence,
  libXtst,
  libgbm,
  nspr,
  nss,
  pango,
  pipewire,
  vulkan-loader,
  wayland, # ozone/wayland
  # Command line programs
  coreutils,
  # Will crash without.
  systemd,
  # Loaded at runtime.
  libexif,
  pciutils,
  # Additional dependencies according to other distros.
  ## Ubuntu
  curl,
  liberation_ttf,
  util-linux,
  wget,
  xdg-utils,
  ## Arch Linux.
  flac,
  harfbuzz,
  icu,
  libpng,
  snappy,
  speechd-minimal,
  ## Gentoo
  bzip2,
  libcap,
  # Necessary for USB audio devices.
  libpulseaudio,
  pulseSupport ? true,
  # For video acceleration via VA-API (--enable-features=VaapiVideoDecoder)
  libva,
  libvaSupport ? true,
  # For Vulkan support (--enable-features=Vulkan)
  addDriverRunpath,
  # For QT support
  # command line arguments which are always set e.g "--disable-gpu"
  commandLineArgs ? "",
}: let
  deps =
    [
      alsa-lib
      at-spi2-atk
      at-spi2-core
      atk
      bzip2
      cairo
      coreutils
      cups
      curl
      dbus
      expat
      flac
      fontconfig
      freetype
      gcc-unwrapped.lib
      gdk-pixbuf
      glib
      harfbuzz
      icu
      libcap
      libdrm
      liberation_ttf
      libexif
      libglvnd
      libkrb5
      libpng
      libX11
      libxcb
      libXcomposite
      libXcursor
      libXdamage
      libXext
      libXfixes
      libXi
      libxkbcommon
      libXrandr
      libXrender
      libXScrnSaver
      libxshmfence
      libXtst
      libgbm
      nspr
      nss
      pango
      pciutils
      pipewire
      snappy
      speechd-minimal
      systemd
      util-linux
      vulkan-loader
      wayland
      wget
    ]
    ++ lib.optionals pulseSupport [libpulseaudio]
    ++ lib.optionals libvaSupport [libva]
    ++ [
      gtk3
      gtk4
    ];
in
  stdenv.mkDerivation (finalAttrs: {
    pname = "helium";
    version = "0.5.5.2";
    src = fetchurl {
      url = "https://github.com/imputnet/helium-linux/releases/download/${finalAttrs.version}/helium-${finalAttrs.version}-x86_64_linux.tar.xz";
      hash = "sha256-EPW7DicU580o3YC+Tffq8AWEpiuydP0HKsUOfh60u1Q=";
    };

    # With strictDeps on, some shebangs were not being patched correctly
    # ie, $out/share/cromite/chrome
    strictDeps = false;

    nativeBuildInputs = [
      makeWrapper
      patchelf
      copyDesktopItems
    ];

    buildInputs = [
      glib
      gtk3
    ];

    rpath = lib.makeLibraryPath deps + ":" + lib.makeSearchPathOutput "lib" "lib64" deps;

    binpath = lib.makeBinPath deps;

    desktopItems = [
      (makeDesktopItem {
        name = "helium";
        exec = "helium %U";
        icon = "helium";
        genericName = "Helium";
        desktopName = "Helium";
        categories = [
          "Application"
          "Network"
          "WebBrowser"
        ];
      })
    ];

    installPhase = ''
      runHook preInstall

      install -Dm644 product_logo_256.png $out/share/pixmaps/helium.png
      cp -v -a . $out/share/helium
      # replace bundled vulkan-loader
      rm -v $out/share/helium/libvulkan.so.1
      ln -v -s -t $out/share/helium ${lib.getLib vulkan-loader}/lib/libvulkan.so.1
      # "--simulate-outdated-no-au" disables auto updates and browser outdated popup
      mkdir $out/bin
      makeWrapper $out/share/helium/chrome $out/bin/helium \
        --prefix LD_LIBRARY_PATH : "$rpath" \
        --prefix PATH            : "$binpath:${lib.makeBinPath [xdg-utils]}" \
        --prefix XDG_DATA_DIRS   : "$XDG_ICON_DIRS:$GSETTINGS_SCHEMAS_PATH:${addDriverRunpath.driverLink}/share" \
        --set CHROME_WRAPPER  "helium" \
        --add-flags "\''${NIXOS_OZONE_WL:+\''${WAYLAND_DISPLAY:+--ozone-platform-hint=auto --enable-features=WaylandWindowDecorations --enable-wayland-ime=true --wayland-text-input-version=3}}" \
        --add-flags "--simulate-outdated-no-au='Tue, 31 Dec 2099 23:59:59 GMT'" \
        --add-flags ${lib.escapeShellArg commandLineArgs}

      # Make sure that libGL and libvulkan are found by ANGLE libGLESv2.so
      patchelf --set-rpath $rpath $out/share/helium/lib*GL*

      for elf in $out/share/helium/{chrome,chrome_crashpad_handler}; do
        patchelf --set-rpath $rpath $elf
        patchelf --set-interpreter "$(cat $NIX_CC/nix-support/dynamic-linker)" $elf
      done

      runHook postInstall
    '';

    passthru.updateScript = ./update.sh;

    meta = {
      changelog = "https://github.com/uazo/cromite/releases";
      description = "Bromite fork with ad blocking and privacy enhancements";
      homepage = "https://github.com/uazo/cromite";
      license = with lib.licenses; [
        bsd3
        gpl3Plus
      ];
      maintainers = with lib.maintainers; [emaryn];
      mainProgram = "helium";
      platforms = ["x86_64-linux"];
      sourceProvenance = with lib.sourceTypes; [binaryNativeCode];
    };
  })
